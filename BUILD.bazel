load("@gazelle//:def.bzl", "gazelle")
load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index", "oci_push", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")
load("//:transition.bzl", "multi_arch")

gazelle(name = "gazelle")

go_binary(
    name = "main",
    out = "grpc_health_proxy",
    embed = [":cmd_lib"],
    visibility = ["//visibility:public"],
    goos = "linux", 
    goarch = "amd64",     
)

go_library(
    name = "cmd_lib",
    srcs = ["main.go"],
    visibility = ["//visibility:private"],
    deps = [
        "@com_github_golang_glog//:go_default_library",
        "@org_golang_google_grpc//:go_default_library",
        "@org_golang_google_grpc//codes:go_default_library",
        "@org_golang_google_grpc//credentials:go_default_library",
        "@org_golang_google_grpc//credentials/insecure:go_default_library",        
        "@org_golang_google_grpc//health/grpc_health_v1:go_default_library",
        "@org_golang_google_grpc//status:go_default_library",
        "@org_golang_x_net//http2:go_default_library",
        "@com_github_gorilla_mux//:go_default_library",
        "@com_github_prometheus_client_golang//prometheus:go_default_library",
        "@com_github_prometheus_client_golang//prometheus/promauto:go_default_library",     
        "@com_github_prometheus_client_golang//prometheus/promhttp:go_default_library",
    ],
)

pkg_tar(
    name = "app-tar",
    srcs = [":main"],
    package_dir = "/",
)

oci_image(
    name = "server-image",
    base = "@distroless_base",
    cmd = [],
    entrypoint = ["/grpc_health_proxy"],
    env = {},
    exposed_ports = [
        "8080/tcp",
    ],
    tars = [
        ":app-tar",
    ],
)

multi_arch(
    name = "images-linux-amd64",
    image = ":server-image",
    platforms = [
        "//:linux_amd64",       
    ],
)

multi_arch(
    name = "images-linux-arm64",
    image = ":server-image",
    platforms = [
        "//:linux_arm64",
    ],
)

oci_image_index(
    name = "index",
    images = [
        ":images-linux-amd64",
        ":images-linux-arm64",      
    ],
)

oci_tarball(
    name = "tar-oci-index",
    format = "oci",
    image = ":index",
    repo_tags = ["index.docker.io/salrashid123/grpc_health_proxy"],
)


oci_push(
    name = "push-image",
    image = ":index",
    remote_tags = ["latest"],
    repository = "index.docker.io/salrashid123/grpc_health_proxy",
)

platform(
    name = "linux_arm",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:aarch64",
    ],
)

platform(
    name = "linux_x86",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

alias(
    name = "linux_amd64",
    actual = ":linux_x86",
    visibility = ["//visibility:public"],
)

alias(
    name = "linux_arm64",
    actual = ":linux_arm",
    visibility = ["//visibility:public"],
)